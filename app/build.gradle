apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId "ru.coolone.travelquest"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 8
        versionName "1.7"

        // Google maps key
        manifestPlaceholders = [googleMapApiKey: GOOGLE_MAPS_API_KEY]
        resValue "string", "GOOGLE_MAPS_API_KEY", GOOGLE_MAPS_API_KEY

        // JUnit
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        dev {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "version_name", variant.versionName
        variant.resValue "string", "version_name_str", "v" + variant.versionName
    }
}

dependencies {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://jitpack.io" }
    }
    ext {
        support = "27.1.1"
        ps = "15.0.0"
        lb = "1.16.20"
        aa = "4.4.0"
    }

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:${support}"
    implementation "com.android.support:design:${support}"
    implementation "com.android.support:support-v4:${support}"
    implementation "com.android.support:cardview-v7:${support}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    implementation "com.google.android.gms:play-services-maps:${ps}"
    implementation "com.google.android.gms:play-services-places:${ps}"
    implementation "com.google.android.gms:play-services-auth:${ps}"

    implementation "com.google.firebase:firebase-auth:${ps}"
    implementation "com.google.firebase:firebase-firestore:${ps}"
    implementation "com.google.firebase:firebase-appindexing:${ps}"

    implementation 'com.android.volley:volley:1.0.0'
    implementation 'com.afollestad:sectioned-recyclerview:0.5.0'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.takisoft.fix:preference-v7:27.0.0.0'
    implementation 'com.seatgeek:placesautocomplete:0.3-SNAPSHOT'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.2.0'

    compileOnly "org.projectlombok:lombok:${lb}"
    annotationProcessor "org.projectlombok:lombok:${lb}"

    annotationProcessor "org.androidannotations:androidannotations:${aa}"
    compile "org.androidannotations:androidannotations-api:${aa}"
}

apply plugin: 'com.google.gms.google-services'

def build_param = "${build}"

if (build_param != "dev") {
    //exclude production build
    android.variantFilter { variant ->
        if (variant.buildType.name == 'dev') {
            variant.setIgnore(true)
        }
    }
} else {
    //exclude all except production build
    android.variantFilter { variant ->
        if (variant.buildType.name != 'dev') {
            variant.setIgnore(true)
        }
    }
}
